plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.20'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'com.zp4rker'
version '1.0.1-release'

def disbotVersion = "1.5.2-release"
def jdaVersion = "4.2.0_221"

repositories {
    mavenCentral()
    maven { url = "https://maven.zp4rker.com" }
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    shadow "com.zp4rker:disbot:$disbotVersion"

    compile "net.dv8tion:JDA:$jdaVersion"
}

compileKotlin {
    kotlinOptions.jvmTarget = 1.8
}

jar {
    manifest {
        attributes "Main-Class": "com.zp4rker.disbot.bootstrap.Main"
        attributes "Class-Path": "lib/"

        attributes "Bot-Main": "com.zp4rker.yasir.MainKt"
        attributes "Implementation-Version": project.version

        attributes "Disbot-Version": disbotVersion
        attributes "JDA-Version": jdaVersion
    }
}

shadowJar {
    archiveClassifier.set("")

    configurations = [project.configurations.shadow]

    minimize {
        exclude(dependency { it.moduleGroup == "com.zp4rker" })
    }

    dependencies {
        exclude(dependency { !project.configurations.shadow.allDependencies.any { d -> it.moduleGroup == d.group && it.moduleName == d.name } })
    }
}

build.dependsOn "shadowJar"


